// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'configurations.gradle'

    ext {
        release = [
                versionName: "5.4.1",
                versionCode: 5041
        ]

        setup = [
                gradleTools: '7.0.2',
                compileSdk : 30,
                buildTools : "31.0.0",
                minSdk     : 16,
                targetSdk  : 30
        ]

        versions = [
                recyclerView    : '1.2.1',
                material        : '1.4.0',
                appcompat       : '1.3.1',
                drawerlayout    : '1.1.0',
                constraintLayout: '2.1.0',
                cardview        : '1.0.0',
                kotlin          : '1.5.30',
                iconics         : '5.2.8',
                materialdrawer  : '8.4.3',
                aboutlib        : '8.9.1',
                roboelectric    : '4.6.1',
                detekt          : '1.18.1',
                paging          : "2.1.2",
                room            : "2.4.0-alpha04",
                lifecycle       : "2.3.1"
        ]
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${setup.gradleTools}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:${versions.aboutlib}"
    }
}

allprojects {
    group "com.mikepenz"

    repositories {
        mavenCentral()
        google()
        maven {
            // (mp) remove when available on maven center
            url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
            content { includeGroup "org.jetbrains.kotlinx" }
        }
    }

    configurations.all {
        resolutionStrategy.force "org.jetbrains.kotlinx:kotlinx-html-jvm:0.7.3"
        // (mp) remove when available on maven center
    }
}

subprojects {
    apply from: "$rootDir/detekt.gradle"

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}"
    }
}

