// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    apply from: 'configurations.gradle'

    ext {
        release = [
                versionName: "5.4.0",
                versionCode: 5040
        ]

        setup = [
                gradleTools: '4.1.3',
                compileSdk : 30,
                buildTools : "30.0.2",
                minSdk     : 16,
                targetSdk  : 30
        ]

        versions = [
                androidX        : '1.1.0',
                recyclerView    : '1.2.0',
                material        : '1.3.0',
                appcompat       : '1.2.0',
                drawerlayout    : '1.1.0',
                constraintLayout: '2.0.4',
                cardview        : '1.0.0',
                kotlin          : '1.4.32',
                iconics         : '5.2.8',
                materialdrawer  : '8.4.0',
                aboutlib        : '8.8.4',
                roboelectric    : '4.5.1',
                detekt          : '1.16.0',
                paging          : "2.1.2",
                room            : "2.2.6",
                lifecycle       : "2.3.1"
        ]
    }

    repositories {
        gradlePluginPortal()
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:${setup.gradleTools}"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:${versions.detekt}"
        classpath "com.mikepenz.aboutlibraries.plugin:aboutlibraries-plugin:${versions.aboutlib}"
    }
}

allprojects {
    group "com.mikepenz"

    repositories {
        mavenCentral()
        google()
        maven {
            // (mp) remove when available on maven center
            url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
            content { includeGroup "org.jetbrains.kotlinx" }
        }
    }

    configurations.all {
        resolutionStrategy.force "org.jetbrains.kotlinx:kotlinx-html-jvm:0.7.3"
        // (mp) remove when available on maven center
    }
}

subprojects {
    apply from: "$rootDir/detekt.gradle"

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:${versions.detekt}"
    }
}

