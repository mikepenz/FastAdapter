apply plugin: 'com.android.application'
apply plugin: 'realm-android'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion setup.compileSdk
    buildToolsVersion setup.buildTools

    defaultConfig {
        minSdkVersion setup.minSdk
        targetSdkVersion setup.targetSdk
        versionCode 3008
        versionName '3.0.0.8-SNAPSHOT'
    }
    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':library')
    compile project(':library-extensions')
    compile project(':library-extensions-expandable')

    compile "com.android.support:cardview-v7:${versions.supportLib}"

    // used to base on some backwards compatible themes
    // contains util classes to support various android versions, and clean up code
    // comes with the awesome "Holder"-Pattern
    // https://github.com/mikepenz/Materialize
    compile 'com.mikepenz:materialize:1.0.3@aar'

    // used to provide out of the box icon font support. simplifies development,
    // and provides scalable icons. the core is very very light
    // https://github.com/mikepenz/Android-Iconics
    compile 'com.mikepenz:iconics-core:2.9.1@aar'
    compile "com.mikepenz:iconics-views:2.9.1@aar"

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    compile('com.mikepenz:materialdrawer:6.0.0.6-SNAPSHOT@aar') {
        transitive = true
        exclude group: "com.mikepenz"
    }
    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    compile 'com.mikepenz:itemanimators:1.0.0@aar'
    //used to generate the Open Source section
    //https://github.com/mikepenz/AboutLibraries
    compile('com.mikepenz:aboutlibraries:6.0.0.6-SNAPSHOT@aar') {
        transitive = true
        exclude group: "com.mikepenz"
    }
    //used to display the icons in the drawer
    //https://github.com/mikepenz/Android-Iconics
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.1@aar'
    compile 'com.mikepenz:community-material-typeface:1.7.22.1@aar'
    compile 'com.mikepenz:google-material-typeface:2.2.0.3.original@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.7.0.0@aar'

    //Used for the StickyHeaderSample
    //https://github.com/timehop/sticky-headers-recyclerview
    //original dependency: compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    //customized version to allow minimal header animation
    compile 'com.mikepenz.thirdparty:stickyheadersrecyclerview:0.4.4-SNAPSHOT@aar'

    //Used to provide the FastScrollBar
    //https://github.com/krimin-killr21/MaterialScrollBar
    compile 'com.turingtechnologies.materialscrollbar:lib:10.0.1'

    //https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:8.7.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.7.0'

    //used to load the images in the ImageListSample
    //https://github.com/bumptech/glide
    compile 'com.github.bumptech.glide:glide:3.8.0'

    //Used to provide the drag selection like google photos
    compile 'com.github.MFlisar:DragSelectRecyclerView:0.2'

    //mopub sdk to showcase the usage of the mopub adapter
    compile('com.mopub:mopub-sdk:4.7.1@aar') {
        transitive = true
    }

    //Used to async operations
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'io.reactivex.rxjava2:rxjava:2.1.2'
}
