apply plugin: 'com.android.application'
//wrap with try and catch so the build is working even if the signing stuff is missing
try {
    apply from: '../../../signing.gradle'
} catch (ex) {
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 23
        versionCode 81
        versionName '0.8.1'

        applicationVariants.all { variant ->
            variant.outputs.each { output ->
                def file = output.outputFile
                def fileName = file.name.replace(".apk", "-v" + versionName + "-c" + versionCode + ".apk")
                output.outputFile = new File(file.parentFile, fileName)
            }
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
            try {
                signingConfig signingConfigs.debug
            } catch (ex) {
            }
            minifyEnabled false
        }
        release {
            try {
                signingConfig signingConfigs.release
            } catch (ex) {
            }
            zipAlignEnabled true
            minifyEnabled false
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile project(':library')

    //used to generate the drawer on the left
    //https://github.com/mikepenz/MaterialDrawer
    compile('com.mikepenz:materialdrawer:5.0.0.fastAdapter.b11-SNAPSHOT@aar') {
        transitive = true
        exclude module: "fastadapter"
    }
    //used to provide different itemAnimators for the RecyclerView
    //https://github.com/mikepenz/ItemAnimators
    compile 'com.mikepenz:itemanimators:0.2.0-SNAPSHOT@aar'
    //used to generate the Open Source section
    //https://github.com/mikepenz/AboutLibraries
    compile('com.mikepenz:aboutlibraries:5.3.6@aar') {
        transitive = true
    }
    //used to display the icons in the drawer
    //https://github.com/mikepenz/Android-Iconics
    compile 'com.mikepenz:material-design-iconic-typeface:2.2.0.1@aar'
    compile 'com.mikepenz:community-material-typeface:1.3.41.1@aar'
    compile 'com.mikepenz:google-material-typeface:2.1.0.1.original@aar'
    compile 'com.mikepenz:fontawesome-typeface:4.5.0.1@aar'

    //Used for the StickyHeaderSample
    //https://github.com/timehop/sticky-headers-recyclerview
    //original dependency: compile 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    //customized version to allow minimal header animation
    compile 'com.mikepenz.thirdparty:stickyheadersrecyclerview:0.4.4-SNAPSHOT@aar'

    //Used to provide the FastScrollBar
    //https://github.com/krimin-killr21/MaterialScrollBar
    compile 'com.turingtechnologies.materialscrollbar:lib:6.1.0'

    //https://github.com/JakeWharton/butterknife
    compile 'com.jakewharton:butterknife:7.0.1'

    //used to load the images in the ImageListSample
    //https://github.com/bumptech/glide
    compile 'com.github.bumptech.glide:glide:3.6.1'
}
